# Maintainer: Nicola Squartini <tensor5@gmail.com>

_ccver=45.0.2454.85-atom-2
_chromiumver=45.0.2454.85
_rebuildcc=0  # 0: don't rebuild libchromiumcontent
pkgname=electron
pkgver=0.35.4
pkgrel=1
pkgdesc='Build cross platform desktop apps with web technologies'
arch=('i686' 'x86_64')
url='http://electron.atom.io/'
license=('MIT' 'custom')
depends=('alsa-lib' 'gconf' 'gtk2' 'libnotify' 'libxtst' 'nss')
makedepends=('clang' 'git' 'libexif' 'ninja' 'npm' 'python2' 'yasm')
source=("git+https://github.com/atom/electron.git#tag=v${pkgver}"
        'git+https://github.com/boto/boto.git'
        'git+https://github.com/atom/brightray.git'
        'breakpad::git+https://github.com/atom/chromium-breakpad.git'
        'git+https://github.com/atom/crashpad.git'
        'git+https://github.com/svn2github/gyp.git'
        "git+https://github.com/atom/libchromiumcontent.git#tag=v${_ccver}"
        'native_mate::git+https://github.com/zcbenz/native-mate.git'
        'git+https://github.com/atom/node.git'
        'git+https://github.com/kennethreitz/requests.git'
        'google-breakpad::git+https://chromium.googlesource.com/external/google-breakpad/src.git'
        "https://github.com/zcbenz/chromium-source-tarball/releases/download/${_chromiumver}/chromium-${_chromiumver}.tar.xz"
        'use-system-clang.patch'
        'use-system-ninja.patch'
        '749363e2c1135bb7f5996f0803ad61db1c8c3925.patch'
        'libchromiumcontent-dont-create-zip.patch'
        'libchromiumcontent-use-system-tools.patch')
noextract=("chromium-${_chromiumver}.tar.xz")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '49cae8a6883943302b4310c7bb9c93803443580c8c959d2f8de3eca28b73dcbd'
            '4377d253749cd2e851870ec499e90a013ce4c5c4833009e785e8e77423bf2b26'
            '6566b7b35fd4adccbf48b1a024a6da6d8b2349149be0eb2374df2e3ee3a777af'
            '3c5eb883adbb9b50ccb720eefe42f4a7e93389986b29a022e8ec47197a7e45c9'
            '17e0b6eea4f9e22572324404d67a075e7c65361811cf5307a3ee63031c998426'
            '0ff3e3061baa0d0df5833da26216fe9ff7b5081d2671b094fe292fc7e85eb995')

prepare() {
  cd ${srcdir}/electron

  patch -Np1 -i ${srcdir}/use-system-clang.patch
  patch -Np1 -i ${srcdir}/use-system-ninja.patch

  mkdir -p ${srcdir}/python2-path
  ln -sf /usr/bin/python2 "${srcdir}/python2-path/python"

  sed -e "s/  update_submodules/  # update_submodules/" -i script/bootstrap.py

  for m in boto breakpad brightray crashpad native_mate node requests; do
    git submodule init vendor/${m}
    git config submodule.vendor/${m}.url ${srcdir}/${m}
  done
  git submodule update

  cd ${srcdir}/electron/vendor/breakpad
  git submodule init src
  git config submodule.src.url ${srcdir}/google-breakpad
  git submodule update

  cd ${srcdir}/electron/vendor/brightray
  sed -e "s/  update_submodules()/  # update_submodules()/" -i script/bootstrap
  for m in libchromiumcontent gyp; do
    git submodule init vendor/${m}
    git config submodule.vendor/${m}.url ${srcdir}/${m}
  done
  git submodule update

  if [ ${_rebuildcc} -ne 0 ]; then
    cd ${srcdir}/libchromiumcontent
    git submodule update --init vendor/python-patch
    patch -Np1 -i "${srcdir}"/749363e2c1135bb7f5996f0803ad61db1c8c3925.patch
    patch -Np1 -i "${srcdir}"/libchromiumcontent-use-system-tools.patch
    patch -Np1 -i "${srcdir}"/libchromiumcontent-dont-create-zip.patch
    cd ${srcdir}/libchromiumcontent/vendor/chromium
    echo 'Extracting chromium source...'
    tar -xJf "${srcdir}"/chromium-${_chromiumver}.tar.xz
    mv chromium-${_chromiumver} src
    if [ ! -e src/.version ]; then
      echo "${_chromiumver}" > src/.version
    fi
  fi
}

build() {
  export PATH="${srcdir}/python2-path:${PATH}"

  if [ "${CARCH}" == 'x86_64' ]; then
    _target=x64
  else
    _target=ia32
  fi

  if [ ${_rebuildcc} -ne 0 ]; then
    echo 'Building libchromiumcontent...'
    cd ${srcdir}/libchromiumcontent
    ##### icudtl.dat
    _use_system=('-Duse_system_expat=1'
                 '-Duse_system_ffmpeg=0'
                 '-Duse_system_flac=1'
                 '-Duse_system_harfbuzz=1'
                 '-Duse_system_icu=0'
                 '-Duse_system_jsoncpp=1'
                 '-Duse_system_libjpeg=1'
                 '-Duse_system_libpng=1'
                 '-Duse_system_libwebp=1'
                 '-Duse_system_libxml=1'
                 '-Duse_system_libxslt=1'
                 '-Duse_system_re2=1'
                 '-Duse_system_snappy=1'
                 '-Duse_system_speex=1'
                 '-Duse_system_zlib=0'
                )
    # vendor/chromium/src/build/linux/unbundle/replace_gyp_files.py \
    #     "${_use_system[@]}"
    _gyp_defines=('clang=1'
                  'clang_use_chrome_plugins=0'
                  'host_clang=0'
                  'remove_webcore_debug_symbols=1'
                  'use_system_fontconfig=1'
                  'use_system_libexif=1'
                  'use_system_yasm=1'
                 )
    sed -e "s/= gyp_defines/= gyp_defines + ' ${_gyp_defines[*]}'/" \
        -i script/update
    (
      export CC='clang -Qunused-arguments'
      export CXX='clang++ -Qunused-arguments'
      script/update --target_arch=${_target}
      sed -e "s|_POSIX_SOURCE|_POSIX_C_SOURCE 200112L|" -i \
          vendor/chromium/src/third_party/boringssl/src/crypto/bio/socket_helper.c
      script/build --target_arch=${_target} --component=static_library
    )
    script/create-dist --target_arch=${_target} --component=static_library
    # Necessary for electron build scripts
    mkdir dist/main/shared_library
    # Save space and time
    find dist/main/static_library -name *.a -exec strip --strip-debug '{}' \;
  fi

  echo 'Building electron...'
  cd ${srcdir}/electron
  _cc=${srcdir}/libchromiumcontent/dist/main
  script/bootstrap.py -v --target_arch=${_target} \
                      --libcc_source_path=${_cc}/src \
                      --libcc_shared_library_path=${_cc}/shared_library \
                      --libcc_static_library_path=${_cc}/static_library
  script/build.py -c Release
}

package() {
  cd ${srcdir}/electron

  _cc=${srcdir}/libchromiumcontent/dist/main

  install -d -m755 ${pkgdir}/usr/share/licenses/electron
  install -m644 LICENSE "${_cc}"/LICENSES.chromium.html \
          ${pkgdir}/usr/share/licenses/electron

  cd out/R
  install -d -m755 ${pkgdir}/usr/lib/electron
  install -m644 \
          content_shell.pak icudtl.dat natives_blob.bin snapshot_blob.bin \
          ${pkgdir}/usr/lib/electron
  install -m755 electron ${pkgdir}/usr/lib/electron
  # namcap warning: Referenced library 'libnode.so' is an uninstalled dependency
  # Fixable by moving libnode.so to /usr/lib
  install -m644 libnode.so ${pkgdir}/usr/lib/electron
  cp -r locales resources ${pkgdir}/usr/lib/electron

  echo -n "v${pkgver}" > ${pkgdir}/usr/lib/electron/version
}
