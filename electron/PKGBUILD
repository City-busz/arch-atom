# Maintainer: Nicola Squartini <tensor5@gmail.com>

_chromiumver=51.0.2704.63
pkgname=electron
pkgver=1.2.0
pkgrel=1
pkgdesc='Build cross platform desktop apps with web technologies'
arch=('i686' 'x86_64')
url='http://electron.atom.io/'
license=('MIT' 'custom')
depends=('ffmpeg' 'gconf' 'gtk2' 'libevent' 'libvpx' 'libxslt' 'libxss'
         'minizip' 'nss' 're2' 'snappy')
makedepends=('clang' 'git' 'gperf' 'harfbuzz-icu' 'jsoncpp' 'libexif'
             'libgnome-keyring' 'libnotify' 'ninja' 'npm' 'pciutils' 'python2'
             'yasm')
source=("git+https://github.com/electron/electron.git#tag=v${pkgver}"
        'git+https://github.com/boto/boto.git'
        'git+https://github.com/electron/brightray.git'
        'breakpad::git+https://github.com/electron/chromium-breakpad.git'
        'git+https://github.com/electron/crashpad.git'
        'git+https://github.com/electron/gyp.git'
        "git+https://github.com/electron/libchromiumcontent.git"
        'native_mate::git+https://github.com/zcbenz/native-mate.git'
        'git+https://github.com/electron/node.git'
        'git+https://github.com/kennethreitz/requests.git'
        'google-breakpad::git+https://chromium.googlesource.com/breakpad/breakpad/src'
        "https://github.com/zcbenz/chromium-source-tarball/releases/download/${_chromiumver}/chromium-${_chromiumver}.tar.xz"
        'dont-bootstrap-libchromiumcontent.patch'
        'dont-use-sysroot.patch'
        'use-system-ninja.patch'
        'use-system-ffmpeg.patch'
        'brightray-use-system-ffmpeg.patch'
        'gyp-no-rpath.patch'
        'libchromiumcontent-static-library-only.patch'
        'libchromiumcontent-use-system-ffmpeg.patch'
        'libchromiumcontent-use-system-tools.patch'
        'chromium-PNGImageDecoder.patch'
        'https://raw.githubusercontent.com/gentoo/gentoo/master/www-client/chromium/files/chromium-system-ffmpeg-r3.patch'
        'minizip.patch'
       )
noextract=("chromium-${_chromiumver}.tar.xz")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '906a36043f7c2cdb8024139cdcf87729f438ddf05af065ab2c0efedb9d7a95ad'
            '14dbd1eecb7034d9e19e1f0c61b0a36ed3b9c610db008ff2a6da2a540ade1221'
            '25aaa517c3acf66a5641a4987ef6f7270b44990cec9822696daeadb4ee22192a'
            'dc3286a1947240ab6ec562263af0b3c9971da2a4ab45970e3e664563e877280f'
            '314d0beb237dafe86bc5a2bd865f591503861cd272b1fca732f5f86ad96c0d89'
            '333af456be29ce44e0d9373b2d0aed4c3ed6a2548d9bde04bc7bf022fdc9cb79'
            '68977a78fb41a549eb449dd35f94e0168d647245516397f0ecdfa7a03a147045'
            'b2a168b4ee8bc1c0b9b48a61a573c7e2cc193ad5a3f4fee845cd3207d2033d3d'
            '121cc5cf6875a8ff8f6e8cece36b7cc70e934c4134c3132b7c9f2da130fe9998'
            '2fb2157c854dab173a2ef9b4aa0ab5bb3c0dd077447c4719c7554eee6e0546b6'
            'caaa63eabcfdced965638b3353448fabda9bc4e6d898d8ec278ecac31502ded1'
            '7d3a300019dce3a9d24ee11f37d918e03f72faf6e9a8ddcc81c6091daecc31a0'
            'ae8db257191600e5d1e3d3142c8b430652047c2b3575d82696573f1649154c62'
           )

prepare() {
  cd ${srcdir}/electron

  patch -Np1 -i ${srcdir}/use-system-ninja.patch
  patch -Np1 -i ${srcdir}/use-system-ffmpeg.patch
  patch -Np1 -i ${srcdir}/dont-use-sysroot.patch
  patch -Np1 -i ${srcdir}/dont-bootstrap-libchromiumcontent.patch

  mkdir -p ${srcdir}/python2-path
  ln -sf /usr/bin/python2 "${srcdir}/python2-path/python"

  sed -e "s/  update_submodules/  # update_submodules/" -i script/bootstrap.py

  for m in boto breakpad brightray crashpad native_mate node requests; do
    git submodule init vendor/${m}
    git config submodule.vendor/${m}.url ${srcdir}/${m}
  done
  git submodule update

  cd ${srcdir}/electron/vendor/breakpad
  git submodule init src
  git config submodule.src.url ${srcdir}/google-breakpad
  git submodule update

  cd ${srcdir}/electron/vendor/brightray
  patch -Np1 -i ${srcdir}/brightray-use-system-ffmpeg.patch
  sed -e "s/  update_submodules()/  # update_submodules()/" -i script/bootstrap
  for m in libchromiumcontent gyp; do
    git submodule init vendor/${m}
    git config submodule.vendor/${m}.url ${srcdir}/${m}
  done
  git submodule update
  # Remove unused shared library and add extra libraries for unbundling
  sed -e "/-lcups/d" \
      -e "s/'-lexpat',/'-lexpat', '<\!@(pkg-config --libs-only-l libavcodec libavformat libavutil libevent flac harfbuzz-icu jsoncpp minizip libpng vpx libwebpdemux libxml-2.0 libxslt)', '-ljpeg', '-lre2', '-lsnappy', '-latomic',/" \
      -i brightray.gyp

  cd ${srcdir}/electron/vendor/brightray/vendor/gyp
  # Remove a useless $ORIGIN/lib/ rpath from the electron binary
  patch -Np1 -i ${srcdir}/gyp-no-rpath.patch

  cd ${srcdir}/electron/vendor/brightray/vendor/libchromiumcontent
  git submodule update --init vendor/python-patch
  patch -Np1 -i "${srcdir}"/libchromiumcontent-use-system-tools.patch
  patch -Np1 -i "${srcdir}"/libchromiumcontent-use-system-ffmpeg.patch
  patch -Np1 -i "${srcdir}"/libchromiumcontent-static-library-only.patch
  rm patches/third_party/ffmpeg/ffmpeg.patch  # Use system ffmpeg
  _chromium_flags=('fastbuild=2'
                   'host_clang=0'
                   'release_extra_cflags="-O3 -Wno-error"'  # -Wno-error required by bundled ICU
                   'remove_webcore_debug_symbols=1'
                   'use_sysroot=0'
                   'use_system_expat=1'
                   'use_system_ffmpeg=1'
                   'use_system_flac=1'
                   'use_system_harfbuzz=1'
                   'use_system_jsoncpp=1'
                   'use_system_libevent=1'
                   'use_system_libjpeg=1'
                   'use_system_libpng=1'
                   'use_system_libvpx=1'
                   'use_system_libwebp=1'
                   'use_system_libxml=1'
                   'use_system_libxslt=1'
                   'use_system_re2=1'
                   'use_system_snappy=1'
                   'use_system_yasm=1'
                   'use_system_zlib=1'
                  )
  cd vendor/chromium
  echo "{" > chromium.gyp_env
  echo "  'GYP_DEFINES': '${_chromium_flags[*]}'" >> chromium.gyp_env
  echo "}" >> chromium.gyp_env
  echo 'Extracting chromium source...'
  tar -xJf "${srcdir}"/chromium-${_chromiumver}.tar.xz
  mv chromium-${_chromiumver} src
  if [ ! -e src/.version ]; then
    echo "${_chromiumver}" > src/.version
  fi
  cd src
  patch -Np1 -i ${srcdir}/chromium-PNGImageDecoder.patch  # Use system libpng
  patch -Np1 -i ${srcdir}/chromium-system-ffmpeg-r3.patch  # Use system ffmpeg
  patch -Np1 -i ${srcdir}/minizip.patch  # Use system minizip
  python2 build/linux/unbundle/replace_gyp_files.py "${_chromium_flags[@]/#/-D}"
}

build() {
  export PATH="${srcdir}/python2-path:${PATH}"

  if [ "${CARCH}" == 'x86_64' ]; then
    _target=x64
  else
    _target=ia32
  fi

  cd ${srcdir}/electron
  LDFLAGS="${LDFLAGS} -Wl,-z,noexecstack"
  script/bootstrap.py --verbose \
                      --target_arch=${_target} \
                      --clang_dir=/usr \
                      --build_libchromiumcontent
  script/build.py -c Release
}

package() {
  cd ${srcdir}/electron

  _cc=${srcdir}/electron/vendor/brightray/vendor/libchromiumcontent/dist/main

  install -d -m755 ${pkgdir}/usr/share/licenses/electron
  install -m644 LICENSE "${_cc}"/LICENSES.chromium.html \
          ${pkgdir}/usr/share/licenses/electron

  cd out/R
  install -d -m755 ${pkgdir}/usr/lib/electron
  install -m644 content_shell.pak icudtl.dat natives_blob.bin snapshot_blob.bin \
          ${pkgdir}/usr/lib/electron
  install -m755 electron ${pkgdir}/usr/lib/electron
  install -d -m755 ${pkgdir}/usr/bin
  ln -s ../lib/electron/electron ${pkgdir}/usr/bin
  # namcap warning: Referenced library 'libnode.so' is an uninstalled dependency
  # Fixable by moving libnode.so to /usr/lib
  install -m644 libnode.so ${pkgdir}/usr/lib/electron
  cp -r locales resources ${pkgdir}/usr/lib/electron

  echo -n "v${pkgver}" > ${pkgdir}/usr/lib/electron/version

  # Install Node headers
  _headers_dest="${pkgdir}/usr/lib/electron/node"
  install -d -m755 "${_headers_dest}"
  cd "${srcdir}"/electron/vendor/node
  find src deps/http_parser deps/zlib deps/uv deps/npm \
    -name "*.gypi" \
      -exec install -D -m644 '{}' "${_headers_dest}/{}" \; \
    -or -name "*.h" \
      -exec install -D -m644 '{}' "${_headers_dest}/{}" \;
  install -m644 {common,config}.gypi "${_headers_dest}"
  cd "${_cc}"/src
  find v8 -name "*.h" \
    -exec install -D -m644 '{}' "${_headers_dest}/deps/{}" \;
  # echo '9' > "${_headers_dest}/installVersion"
}
