# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=atom
pkgver=1.7.3
pkgrel=2
pkgdesc='A hackable text editor for the 21st Century'
arch=('i686' 'x86_64')
url='https://github.com/atom/atom'
license=('MIT' 'custom')
depends=('electron'
         'nodejs-atom-package-manager')
makedepends=('npm')
conflicts=('atom-editor' 'atom-editor-bin' 'atom-editor-git')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz"
        'deprecated-api.patch'
        'fix-atom-sh.patch'
        'fix-license-path.patch'
        'use-system-apm.patch'
        'use-system-electron.patch')
sha256sums=('5074b59ddaca5525eb48098dee6fe63013799cbc77749add314b9e1bc894b8f4'
            'd70c4b06631dd451f020366c834d3102cea6837c2e935e66840e0aca3654cea2'
            'ac409d709fd1090fda93b6e575cf46f866d1f9f914a48ea94eb01af3d5c02b9e'
            'bb6ac5c57196cd707a8ab61aa7f30fe2d77b5e6f02fd02ad96afb7de81702046'
            '19ba93a502ff0c9db6e197f09eda9881999e4d80c264bc40d60f96e9423dd1a9'
            'dd052d5d757701082a47bce1b752074f1d5148ef0f94eade0b00118154848c6a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/fix-atom-sh.patch
  patch -Np1 -i "${srcdir}"/use-system-electron.patch
  patch -Np1 -i "${srcdir}"/use-system-apm.patch
  patch -Np1 -i "${srcdir}"/fix-license-path.patch

  # apm with system (updated) nodejs cannot 'require' modules inside asar
  sed -e "s/, 'generate-asar'//" -i build/Gruntfile.coffee

  # Fix for Electron 1.0.0
  patch -Np1 -i "${srcdir}"/deprecated-api.patch
  sed -e 's/"settings-view": "0.235.1"/"settings-view": "0.236.0"/' \
      -i package.json
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export ATOM_RESOURCE_PATH="$srcdir/atom-$pkgver"
  # If unset, ~/.atom/.node-gyp/.atom/.npm is used
  export NPM_CONFIG_CACHE="${HOME}/.atom/.npm"

  # Make sure NodeGit builds from source and with the correct runtime
  export BUILD_ONLY=1
  ELECTRON_VERSION=$(</usr/lib/electron/version)
  export ELECTRON_VERSION=${ELECTRON_VERSION#v}

  apm clean
  apm install

  echo 'Removing NodeGit devDependencies...'
  cd node_modules/nodegit
  npm prune --production
  cd ../..

  _packagesToDedupe=('abbrev'
                     'amdefine'
                     'atom-space-pen-views'
                     'cheerio'
                     'domelementtype'
                     'fs-plus'
                     'grim'
                     'highlights'
                     'humanize-plus'
                     'iconv-lite'
                     'inherits'
                     'loophole'
                     'oniguruma'
                     'q'
                     'request'
                     'rimraf'
                     'roaster'
                     'season'
                     'sigmund'
                     'semver'
                     'through'
                     'temp')
  apm dedupe ${_packagesToDedupe[@]}
  cd build
  npm install
  cd ..
  script/grunt --channel=stable
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m 755 "${pkgdir}"/usr/lib
  cp -r out/Atom/resources/app "${pkgdir}"/usr/lib
  mv "${pkgdir}"/usr/lib/app "${pkgdir}"/usr/lib/atom

  install -d -m 755 "${pkgdir}/usr/share/applications"
  sed -e "s|<%= appName %>|Atom|" \
      -e "s/<%= description %>/${pkgdesc}/" \
      -e "s|<%= installDir %>/share/<%= appFileName %>/atom|/usr/lib/electron/electron --app=/usr/lib/atom|" \
      -e "s|<%= iconPath %>|/usr/lib/atom/resources/atom.png|" \
      resources/linux/atom.desktop.in > "${pkgdir}/usr/share/applications/atom.desktop"

  install -D -m 755 out/Atom/resources/new-app/atom.sh "${pkgdir}/usr/bin/atom"

  install -D -m 755 out/Atom/resources/LICENSE.md \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"

  # Remove useless stuff
  find "${pkgdir}"/usr/lib/atom/node_modules \
      -name "*.a" -exec rm '{}' \; \
      -or -name "*.bat" -exec rm '{}' \; \
      -or -name "benchmark" -prune -exec rm -r '{}' \; \
      -or -name "doc" -prune -exec rm -r '{}' \; \
      -or -name "html" -prune -exec rm -r '{}' \; \
      -or -name "man" -prune -exec rm -r '{}' \; \
      -or -path "*/less/gradle" -prune -exec rm -r '{}' \; \
      -or -path "*/task-lists/src" -prune -exec rm -r '{}' \;
}
