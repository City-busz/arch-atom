# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=nodejs-atom-package-manager
pkgver=1.7.1
pkgrel=2
pkgdesc='Atom package manager'
arch=('i686' 'x86_64')
url='https://github.com/atom/apm'
license=('MIT')
groups=('atom')
depends=('libgnome-keyring' 'nodejs' 'python2')
makedepends=('npm')
options=(!emptydirs)
source=("https://github.com/atom/apm/archive/v${pkgver}.tar.gz"
        'electron-version.patch'
        'use-system-nodejs.patch')
sha256sums=('7001da4ed327b3e52a4d16342649124243eec87d4d4a277524a651550c29c3c6'
            'eb72cc1c35697b85be11ae9e7d08a648c16c7d0e6fec034e00cffbe465e006e7'
            '66dfc84b5cd76dd02ef6cf04d532cdb2ff6a6eaa4d94bf47ab5ed81d9e13326c')

prepare() {
  rm -rf "${srcdir}"/apm-build

  cd apm-${pkgver}

  sed -e '/"install": "node .\/script\/download-node.js",/d' -i package.json
  rm -r script

  # keytar-3.0.1 pins the version of nan to 2.0.0, and fails to compile with
  # nodejs-6.0.0
  sed -e 's|"keytar": "^3.0"|"keytar": "3.0.0"|' -i package.json
}

build() {
  cd apm-${pkgver}

  npm install --only=dev
  npm install --user root -g --prefix="${srcdir}"/apm-build/usr
}

package() {
  cp -r "${srcdir}"/apm-build/usr "${pkgdir}"

  _apmdir='/usr/lib/node_modules/atom-package-manager'

  cd "${pkgdir}"${_apmdir}
  install -m644 "${srcdir}"/apm-${pkgver}/README.md ./
  patch -Np1 -i ${srcdir}/electron-version.patch
  patch -Np1 -i ${srcdir}/use-system-nodejs.patch
  rm bin/apm.cmd

  # Fix location of Atom app
  sed -e 's|share/atom/resources/app.asar|lib/atom|g' \
      -i "${pkgdir}"${_apmdir}/lib/apm.js

  # Install license file
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/atom-package-manager/LICENSE.md \
     "${pkgdir}/usr/share/licenses/${pkgname}"

  # Remove occurrences of ${srcdir}
  find "${pkgdir}" -name "package.json" \
       -exec sed -e "s|${srcdir}/apm-build||" \
                 -e "s|${srcdir}/apm-${pkgver}|${_apmdir}|" \
                 -i '{}' \;

  # Remove useless stuff and use python2
  find "${pkgdir}"/usr/lib \
      -name ".*" -prune -exec rm -r '{}' \; \
      -or -name "*.a" -exec rm '{}' \; \
      -or -name "*.bat" -exec rm '{}' \; \
      -or -name "*.mk" -exec rm '{}' \; \
      -or -path "*/git-utils/binding.gyp" -exec rm '{}' \; \
      -or -path "*/git-utils/src" -prune -exec rm -r '{}' \; \
      -or -path "*/keytar/binding.gyp" -exec rm '{}' \; \
      -or -path "*/keytar/src" -prune -exec rm -r '{}' \; \
      -or -path "*/oniguruma/binding.gyp" -exec rm '{}' \; \
      -or -path "*/oniguruma/src" -prune -exec rm -r '{}' \; \
      -or -name "benchmark" -prune -exec rm -r '{}' \; \
      -or -name "binding.Makefile" -exec rm '{}' \; \
      -or -name "config.gypi" -exec rm '{}' \; \
      -or -name "deps" -prune -exec rm -r '{}' \; \
      -or -name "doc" -prune -exec rm -r '{}' \; \
      -or -name "html" -prune -exec rm -r '{}' \; \
      -or -name "Makefile" -exec rm '{}' \; \
      -or -name "man" -prune -exec rm -r '{}' \; \
      -or -name "obj.target" -prune -exec rm -r '{}' \; \
      -or -name "samples" -prune -exec rm -r '{}' \; \
      -or -name "test" -prune -exec rm -r '{}' \; \
      -or -name "tests" -prune -exec rm -r '{}' \; \
      -or -name "*.py" -exec \
          sed -e 's|^#!/usr/bin/env python$|#!/usr/bin/python2|' -i '{}' \; \
      -or -path "*/gyp/gyp" \
          -exec sed -e 's|exec python |exec python2 |' -i '{}' \;
}
